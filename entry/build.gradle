//打包hap包的插件
apply plugin: 'com.huawei.ohos.hap'
apply plugin: 'com.huawei.ohos.decctest'

//加载签名信息配置文件
def signingConfigPropsFile = rootProject.file("signing-config.properties")
def signingConfigProps = new Properties()
signingConfigProps.load((new FileInputStream(signingConfigPropsFile)))

//For instructions on signature configuration, see https://developer.harmonyos.com/cn/docs/documentation/doc-guides/ide_debug_device-0000001053822404#ZH-CN_TOPIC_0000001154985555__section1112183053510
ohos {
    //应用编译构建的目标SDK版本
    compileSdkVersion 6
    defaultConfig {
        //应用兼容的最低SDK版本
        compatibleSdkVersion 6
    }

    //配置签名信息索引
    signingConfigs {
        release {
            storeFile file(signingConfigProps['storeFile'])
            storePassword signingConfigProps['storePassword']
            keyAlias signingConfigProps['keyAlias']
            keyPassword signingConfigProps['keyPassword']
            signAlg = 'SHA256withECDSA'
            profile file(signingConfigProps['profile'])
            certpath file(signingConfigProps['certpath'])
        }
    }

    buildTypes {
        //构建Release类型的hap包或APP时的混淆功能设置
        release {
            proguardOpt {
                //开启代码混淆功能
                proguardEnabled true
                // 配置混淆规则文件相对路径
                rulesFiles 'proguard-rules.pro'
            }

            signingConfig signingConfigs.release
        }

        debug {
            signingConfig signingConfigs.release
        }
    }

    externalNativeBuild {
        path "src/main/cpp/CMakeLists.txt"   //CMake配置入口，提供CMake构建脚本的相对路径
        arguments "-v"    //传递给CMake的可选编译参数
        abiFilters "arm64-v8a"     //用于设置本机的ABI编译环境
        cppFlags ""   //设置C++编译器的可选参数
    }

    //合并JS代码打包时，请在ohos闭包下手动添加，true表示需要合并JS代码，false表示不需要合并JS代码
    mergeJsSrc true

    //是否支持数据库注释
    compileOptions {
        annotationEnabled true   //true表示支持，false表示不支持
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.har'])
    testImplementation 'junit:junit:4.13'
    ohosTestImplementation 'com.huawei.ohos.testkit:runner:1.0.0.100'

    implementation project(":har1")
    implementation project(":javaLib1")
}
decc {
    supportType = ['html', 'xml']
}
